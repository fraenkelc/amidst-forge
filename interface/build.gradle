plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow'
}

def grpcVersion = '1.26.0'
def flatbuffers_version = '1.11.0'

version = rootProject.version
group = rootProject.group

configurations {
    flatbuffers_compiler
    compile.resolutionStrategy.force "io.grpc:grpc-core:$grpcVersion"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    main {
        java {
            srcDir "src/generated/java"
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    compileOnly "javax.annotation:javax.annotation-api:1.2"

    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.flatbuffers:flatbuffers-java-grpc:$flatbuffers_version"
    implementation "com.google.flatbuffers:flatbuffers-java:$flatbuffers_version"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:1.9.5"
}

//
def compilerFile = file("$buildDir/flatc_windows_exe.zip")

task downloadCompiler {
    outputs.files compilerFile
    doLast {
        compilerFile << new URL("https://github.com/google/flatbuffers/releases/download/v1.11.0/flatc_windows_exe.zip").getBytes()
    }
}

task unpackCompiler(type: Copy) {
    dependsOn downloadCompiler
    destinationDir = file("$buildDir/tmp/flatc")
    from({ zipTree(compilerFile) })
}

task createFlatBuffers() {
    inputs.files(fileTree("src/main/fbs").include("**/*.fbs"))
    outputs.dir("src/generated/java")
    outputs.upToDateWhen { false }
    group = "Generation"
    dependsOn tasks["unpackCompiler"]
    def outputDir = file("src/generated/java")

    doLast {
        delete { del ->
            del.delete(fileTree(outputDir).include("**/*"))
        }
        inputs.files.each { inputFile ->
            project.exec { cmd ->
                cmd.workingDir = projectDir
                cmd.executable "$buildDir/tmp/flatc/flatc.exe"
                cmd.args "--grpc", "--java", "-o", outputDir, "$inputFile"
            }
        }
    }
}


dependencies {
    flatbuffers_compiler group: 'com.github.davidmoten', name: 'flatbuffers-compiler', version: '1.9.0.1', classifier: 'distribution-windows', ext: 'zip'
}
