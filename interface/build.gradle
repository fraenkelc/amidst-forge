plugins {
    id 'java-library'
}

configurations {
	sbeTool
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task sbeGenerateJavaCodecs (type:JavaExec) {
	outputs.dir "$buildDir/generated/sbe"
	inputs.file 'src/main/sbe/amidstinterface.xml'
    main = 'uk.co.real_logic.sbe.SbeTool'
    classpath = configurations.sbeTool
    systemProperties(
        'sbe.output.dir': "$buildDir/generated/sbe",
        'sbe.xinclude.aware': 'true',
        'sbe.target.language': 'Java')
    args = ['src/main/sbe/amidstinterface.xml']
    doFirst {
    	delete fileTree("$buildDir/generated/sbe")
    }
}

compileJava.dependsOn sbeGenerateJavaCodecs

sourceSets {
    main {
        java {
        	srcDir "$buildDir/generated/sbe"
        }
    }
}


dependencies {
	api 'org.agrona:agrona:1.0.7'

    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:1.9.5"
    
    sbeTool "uk.co.real-logic:sbe-tool:1.13.2"
}
